module(name = "capture-sdk")

####################
# Constants
####################

RUST_VERSION = "1.90.0"

OKHTTP_VERSION = "4.12.0"

LIFECYCLE_VERSION = "2.8.7"

COMPOSE_VERSION = "1.7.6"

COMPOSE_COMPILER_VERSION = "1.5.15"

KOTLIN_COMPILE_VERSION = "2.1.21"

KOTLIN_COMPILER_SHA = "6ab72d6144e71cbbc380b770c2ad380972548c63ab6ed4c79f11c88f2967332e"

KOTLIN_STD_VERSION = "1.9.25"

NDK_API_LEVEL = 21

SDK_API_LEVEL = 36

SDK_BUILD_TOOLS_VERSION = "36.1.0"

####################
# Bazel dependencies
####################

http_jar = use_repo_rule("@bazel_tools//tools/build_defs/repo:http.bzl", "http_jar")

http_archive = use_repo_rule("@bazel_tools//tools/build_defs/repo:http.bzl", "http_archive")

new_git_repository = use_repo_rule("@bazel_tools//tools/build_defs/repo:git.bzl", "new_git_repository")

bazel_dep(name = "rules_robolectric", version = "4.14.1.2")
bazel_dep(name = "rules_multirun", version = "0.10.0")
bazel_dep(name = "apple_support", version = "1.22.1", repo_name = "build_bazel_apple_support")
bazel_dep(name = "rules_swift", version = "2.9.0")
bazel_dep(name = "bazel_skylib", version = "1.7.1")
bazel_dep(name = "rules_xcodeproj", version = "2.12.1")
bazel_dep(name = "rules_apple", version = "4.1.1")
bazel_dep(name = "rules_pkg", version = "1.0.1")
bazel_dep(name = "rules_cc", version = "0.1.1")
bazel_dep(name = "rules_java", version = "8.11.0")
bazel_dep(name = "rules_android", version = "0.6.4")
bazel_dep(name = "rules_android_ndk", version = "0.1.3")
bazel_dep(name = "rules_jvm_external", version = "6.6")
bazel_dep(name = "rules_detekt", version = "0.8.1.2")
bazel_dep(name = "flatbuffers", version = "25.2.10")
bazel_dep(name = "platforms", version = "0.0.11")
bazel_dep(name = "rules_kotlin", version = "2.1.9")
bazel_dep(name = "rules_proto", version = "7.1.0")
bazel_dep(name = "protobuf", version = "29.3")
bazel_dep(name = "rules_rust", version = "0.65.0")
bazel_dep(name = "rules_shell", version = "0.4.0")

# Required by android_artifacts for pom_file, it's not in bazel central registry
bazel_dep(name = "google_bazel_common", version = "0.0.1")

http_jar(
    name = "bazel_diff",
    integrity = "sha256-Sn3WTHsfsSGbglE4xiwgbSrOdHCqXToHpVRTLnQEiJM=",
    url = "https://github.com/Tinder/bazel-diff/releases/download/9.0.3/bazel-diff_deploy.jar",
)

new_git_repository(
    name = "bitdrift_api",
    build_file = "//bazel:BUILD.bitdriftlabs_api",
    commit = "250f755b39343c90742250a1ae8d9f73b8423df7",
    remote = "https://github.com/bitdriftlabs/api.git",
)

new_git_repository(
    name = "kscrash",
    build_file = "//bazel:BUILD.kscrash",
    commit = "f0241a44f723a6b29c9db2a303917e89256a2557",
    remote = "https://github.com/kstenerud/KSCrash.git",
)

git_override(
    module_name = "google_bazel_common",
    commit = "c35b0339ae7d7ac95761f69f4a0eed033163cc80",
    remote = "https://github.com/google/bazel-common",
)

http_archive(
    name = "Difference",
    build_file = "@//bazel/third_party:Difference.BUILD",
    sha256 = "7ffc131fd19b7c1ea390d186b942aa6b7ff732add9e6df34281fe9f777656968",
    strip_prefix = "Difference-1.1.0",
    urls = ["https://github.com/krzysztofzablocki/Difference/archive/1.1.0.tar.gz"],
)

http_archive(
    name = "SwiftBenchmark",
    build_file = "@//bazel/third_party:SwiftBenchmark.BUILD",
    sha256 = "9c5bccfbddaeed7d3aa731118644655c0e550ab2267e1a3238ca0daa06ade0f9",
    strip_prefix = "swift-benchmark-0.1.2",
    urls = ["https://github.com/google/swift-benchmark/archive/0.1.2.tar.gz"],
)

http_archive(
    name = "SwiftArgumentParser",
    build_file = "@//bazel/third_party:SwiftArgumentParser.BUILD",
    sha256 = "946a4cf7bdd2e4f0f8b82864c56332238ba3f0a929c6d1a15f55affdb10634e6",
    strip_prefix = "swift-argument-parser-1.5.0",
    urls = ["https://github.com/apple/swift-argument-parser/archive/1.5.0.tar.gz"],
)

http_archive(
    name = "DrString",
    build_file_content = """exports_files(["drstring"])""",
    sha256 = "99833f0ad3d9f3bbf40aee88c6f959a0a9b03e3b7b34e2fca660644e13c1f3ef",
    url = "https://github.com/dduan/DrString/releases/download/0.6.1/drstring-universal-apple-darwin.tar.gz",
)

http_archive(
    name = "DrString_Linux",
    build_file_content = """
load("@bazel_skylib//rules:native_binary.bzl", "native_binary")
native_binary(
    name = "DrString_Linux",
    src = "usr/bin/drstring",
    out = "usr/bin/drstring",
    visibility = ["//visibility:public"],
)
    """,
    sha256 = "50cbf3dbb9b675d7bd179b02e7925f3729d2e0c8c9fd4df09ea1cd2f15c50205",
    url = "https://github.com/dduan/DrString/releases/download/0.6.1/drstring-x86_64-unknown-ubuntu.tar.gz",
)

####################
# Android extensions
####################

android_sdk_repository_extension = use_extension("@rules_android//rules/android_sdk_repository:rule.bzl", "android_sdk_repository_extension")
android_sdk_repository_extension.configure(
    api_level = SDK_API_LEVEL,
    build_tools_version = SDK_BUILD_TOOLS_VERSION,
)

android_ndk_repository_extension = use_extension("@rules_android_ndk//:extension.bzl", "android_ndk_repository_extension")
android_ndk_repository_extension.configure(api_level = NDK_API_LEVEL)
use_repo(android_ndk_repository_extension, "androidndk")

register_toolchains("@androidndk//:all")

register_toolchains("//:kotlin_toolchain")

maven = use_extension("@rules_jvm_external//:extensions.bzl", "maven")
maven.install(
    aar_import_bzl_label = "@rules_android//rules:rules.bzl",
    artifacts = [
        "com.squareup.okio:okio:3.10.2",
        "com.squareup.okio:okio-jvm:3.10.2",
        "com.google.code.findbugs:jsr305:3.0.2",

        # Dokka (javadocs generator)
        "org.jetbrains.dokka:analysis-kotlin-descriptors:1.9.10",
        "org.jetbrains.dokka:dokka-base:1.9.10",
        "org.jetbrains.dokka:dokka-cli:1.9.10",

        # Library dependencies
        "com.google.code.gson:gson:2.10.1",
        "com.squareup.okhttp3:okhttp:{}".format(OKHTTP_VERSION),
        "androidx.startup:startup-runtime:1.2.0",
        "androidx.core:core:1.13.1",
        "com.google.guava:listenablefuture:1.0",  #required by androidx.lifecycle:lifecycle-process below
        "androidx.lifecycle:lifecycle-common:{}".format(LIFECYCLE_VERSION),
        "androidx.lifecycle:lifecycle-process:{}".format(LIFECYCLE_VERSION),
        "org.jetbrains.kotlin:kotlin-stdlib:{}".format(KOTLIN_STD_VERSION),
        "androidx.emoji2:emoji2:1.5.0",
        "androidx.collection:collection:1.4.5",
        "androidx.metrics:metrics-performance:1.0.0-beta02",
        "com.google.flatbuffers:flatbuffers-java:25.2.10",
        "com.google.protobuf:protobuf-kotlin-lite:4.31.1",

        # Compose
        "org.jetbrains.kotlin:kotlin-compose-compiler-plugin-embeddable:{}".format(KOTLIN_COMPILE_VERSION),
        "org.jetbrains:annotations:26.0.1",
        "androidx.core:core-ktx:1.13.1",
        "androidx.appcompat:appcompat:1.7.0",
        "androidx.activity:activity-compose:1.9.3",
        "androidx.compose.material:material:{}".format(COMPOSE_VERSION),
        "androidx.compose.ui:ui:{}".format(COMPOSE_VERSION),
        "androidx.compose.ui:ui-tooling:{}".format(COMPOSE_VERSION),
        "androidx.compose.runtime:runtime:{}".format(COMPOSE_VERSION),
        "androidx.annotation:annotation-jvm:1.9.1",

        # Compile-only via wrapper target; not on runtime classpath
        "com.squareup.retrofit2:retrofit:3.0.0",
    ],
    repositories = [
        "https://maven.google.com",
        "https://repo1.maven.org/maven2",
    ],
    use_starlark_android_rules = True,
    version_conflict_policy = "pinned",
)

# Test artifacts
maven.artifact(
    testonly = True,
    artifact = "mockito-core",
    group = "org.mockito",
    version = "4.11.0",
)
maven.artifact(
    testonly = True,
    artifact = "mockito-inline",
    group = "org.mockito",
    version = "4.11.0",
)
maven.artifact(
    testonly = True,
    artifact = "mockito-kotlin",
    group = "com.nhaarman.mockitokotlin2",
    version = "2.2.0",
)
maven.artifact(
    testonly = True,
    artifact = "core",
    group = "androidx.test",
    version = "1.6.0",
)
maven.artifact(
    testonly = True,
    artifact = "robolectric",
    group = "org.robolectric",
    version = "4.14",
)
maven.artifact(
    testonly = True,
    artifact = "android-all",
    group = "org.robolectric",
    version = "15-robolectric-12650502",
)
maven.artifact(
    testonly = True,
    artifact = "assertj-core",
    group = "org.assertj",
    version = "3.22.0",
)
maven.artifact(
    testonly = True,
    artifact = "mockwebserver",
    group = "com.squareup.okhttp3",
    version = OKHTTP_VERSION,
)
maven.artifact(
    testonly = True,
    artifact = "junit",
    group = "junit",
    version = "4.13.2",
)
maven.artifact(
    testonly = True,
    artifact = "classgraph",
    group = "io.github.classgraph",
    version = "4.8.146",
)
use_repo(maven, "maven")

####################
# Rust extensions
####################

single_version_override(
    module_name = "rules_rust",
    patches = ["//bazel/third_party/patches:rules_rust.patch"],
)

rust = use_extension("@rules_rust//rust:extensions.bzl", "rust")
rust.toolchain(
    # Rules rust defaults to using an allocator library that is outdated. One would hope you can set your own here
    # but I couldn't find a way to pass it in, since all lables here are local to rules_rust (ie you can't pass //core/alloc).
    allocator_library = "@rules_rust//ffi/cc/allocator_library:noop",
    extra_target_triples = [
        "aarch64-apple-ios-sim",
        "aarch64-apple-ios",
        "aarch64-linux-android",
        "armv7-linux-androideabi",
        "i686-linux-android",
        "x86_64-apple-ios",
        "x86_64-linux-android",
    ],
    rustfmt_version = "nightly/2025-03-05",
    # We need this shas, since these archives are generated by us and we want to make sure we always use these as opposed
    # to the official ones. There is a `rust_std_checksum.sh` script that generates these shas in the `tools` directory.
    # For security reasons, we include all shas here to make sure that a malicious actor with access to rust-std-mobile
    # can't include a compromised tool.
    #
    # tl;dr; run e.g. $ ./tools/rust_std_checksum.sh 1.82.0
    sha256s = {
        "rust-std-" + RUST_VERSION + "-aarch64-apple-ios-sim.tar.gz": "7cfa5f18307d198da5e28fa9c7dc129cb8856131cf1121fed91a756c2a7b3580",
        "rust-std-" + RUST_VERSION + "-aarch64-apple-ios.tar.gz": "596585ba45607c0487cec70ba1b9b3215ef6826de1bd59a44273b4f3f442f3b2",
        "rust-std-" + RUST_VERSION + "-x86_64-apple-ios.tar.gz": "e0e42c0139ccd113f7b8c671071aa6c9999208a932847eefdcdc2b3ea8099524",
        "rust-std-" + RUST_VERSION + "-aarch64-linux-android.tar.gz": "0094d52b27d0c3091000f9c439b683a205c15cbd713acafd1a8febee42ed448a",
        "rust-std-" + RUST_VERSION + "-armv7-linux-androideabi.tar.gz": "1ce1491fd05ddd65436a130d0a84dc4230f542bef838c16c61375f94dcf1b727",
        "rust-std-" + RUST_VERSION + "-i686-linux-android.tar.gz": "40c03f9fd057fdcb28223166ae22ebf667ae76727773fd3c33eeb1088ded9509",
        "rustc-" + RUST_VERSION + "-aarch64-apple-darwin.tar.gz": "77e8d4c354aab89b2abcd4608d2cb7612a99000d8064491aeafbc325c757943a",
        "cargo-" + RUST_VERSION + "-aarch64-apple-darwin.tar.gz": "a242bb8d88d4d1b621f33742d0c718ebb5a551b07d21240e8b8c31152dc29737",
        "llvm-tools-" + RUST_VERSION + "-aarch64-apple-darwin.tar.gz": "d89589963e941955aeb65ce4e71f88322686f7e4d65f66a87a05c8b893a6cf78",
        "rust-std-" + RUST_VERSION + "-aarch64-apple-darwin.tar.gz": "30d6e3008a288f779c90c92d867dcd4f05fd5107d7f3e69d4d37a5945878110a",
        "clippy-" + RUST_VERSION + "-aarch64-apple-darwin.tar.gz": "323a3ebca03d5aa7dc8d8d54ac05963e08a1c4fb22066ef6b05ee5398e397b12",
        "rustfmt-" + RUST_VERSION + "-aarch64-apple-darwin.tar.gz": "0fff92769fbe393468584232a30ae8e0ea01eb2242201122eea56593b30861ac",
        "rustc-" + RUST_VERSION + "-x86_64-unknown-linux-gnu.tar.gz": "b22d65fd75f50cc036c0cb514500628253aaa815bf2e18ec65620acb8a1ad244",
        "cargo-" + RUST_VERSION + "-x86_64-unknown-linux-gnu.tar.gz": "dc0f70c6e681776d0c5e01953b50488e8b2f972eed5aece6d094e497ea480ab0",
        "llvm-tools-" + RUST_VERSION + "-x86_64-unknown-linux-gnu.tar.gz": "6c4a9d631daee2f182b891694e83b6151eea5d9d3ac35917089fde8da42e43d8",
        "rust-std-" + RUST_VERSION + "-x86_64-unknown-linux-gnu.tar.gz": "81d7dad58adaf8a9904771d1aa1ea7e8dc732236fe0a1b148a068ceb502de7fb",
        "clippy-" + RUST_VERSION + "-x86_64-unknown-linux-gnu.tar.gz": "0e1db6adef940dd21896dbd7a051e25056df2fede90591c583552f94e913af22",
        "rustfmt-" + RUST_VERSION + "-x86_64-unknown-linux-gnu.tar.gz": "61407015edaeafa9b61b3ffefb5077624cd4017ac50be05e246bcadafbb56d99",
    },
    urls = [
        # NOTE: `urls` are technically mirrors so we want to make sure we always try our own first then the official ones.
        # We'll ensure that the ones we want to serve always come from us by the previous sha256s dictionary. Please ensure
        # that the extensions on both of these are the same.
        "https://github.com/bitdriftlabs/rust-std-mobile/releases/download/" + RUST_VERSION + "/{}.tar.gz",

        # We need this because we only serve std for mobile archs but rustc, clippy, cargo, llvm-tools and even std for
        # apple-darwin/linux are served from the official mirror.
        "https://static.rust-lang.org/dist/{}.tar.gz",
    ],
    versions = [
        RUST_VERSION,
        "nightly/2025-10-02",
    ],
)
use_repo(rust, "rust_toolchains")

crate = use_extension("@rules_rust//crate_universe:extensions.bzl", "crate")
crate.from_cargo(
    name = "crates",
    cargo_config = "//:Cargo.toml",
    cargo_lockfile = "//:Cargo.lock",
    # This trades the chance that the registry gets corrupted for speed when repinning.
    isolated = False,
    lockfile = "//:Cargo.Bazel.lock",
    manifests = [
        "//:Cargo.toml",
        "//proto:Cargo.toml",
        "//platform/jvm:Cargo.toml",
        "//platform/jvm/core:Cargo.toml",
        "//test/platform/jvm:Cargo.toml",
        "//platform/shared:Cargo.toml",
        "//platform/swift/source:Cargo.toml",
        "//platform/test_helpers:Cargo.toml",
        "//test/platform/pom_checker:Cargo.toml",
        "//test/platform/swift/bridging:Cargo.toml",
        "//test/benchmark:Cargo.toml",
    ],
    supported_platform_triples = [
        "aarch64-apple-ios-sim",
        "aarch64-apple-ios",
        "aarch64-linux-android",
        "armv7-linux-androideabi",
        "i686-linux-android",
        "x86_64-apple-ios",
        "x86_64-linux-android",
        "aarch64-apple-darwin",
        "x86_64-unknown-linux-gnu",
    ],
)

# File generation must be skipped because of https://github.com/bazelbuild/rules_rust/issues/3552
crate.annotation(
    build_script_env = {"SKIP_PROTO_GEN": "1"},
    crate = "bd-grpc",
)
crate.annotation(
    build_script_env = {"SKIP_PROTO_GEN": "1"},
    crate = "bd-pgv",
)
crate.annotation(
    build_script_env = {"SKIP_PROTO_GEN": "1"},
    crate = "bd-proto",
)
crate.annotation(
    build_script_env = {"SKIP_FILE_GEN": "1"},
    crate = "bd-report-writer",
)
crate.annotation(
    build_script_env = {"SKIP_FILE_GEN": "1"},
    crate = "bd-bonjson-ffi",
)

# A recent rustix update seems to have broken something here, so manually add in the crates we need to build under Bazel.
crate.annotation(
    crate = "linux-raw-sys",
    crate_features = [
        "errno",
        "std",
        "general",
        "ioctl",
    ],
)

use_repo(crate, "crates")
